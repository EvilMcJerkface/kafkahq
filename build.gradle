buildscript {
    ext {
        micronautVersion = "1.1.0.M1"
        joobyVersion = "1.5.+"
        confluentVersion = "5.1.0"
        kafkaVersion = "2.1.+"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        // jooby
        classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "org.jooby:jooby-gradle-plugin:$joobyVersion"

        // kafkahq
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.16.0"
    }
}

plugins {
    // micronaut
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "application"
    id "java"
    id "net.ltgt.apt-eclipse" version "0.20"
    id "net.ltgt.apt-idea" version "0.20"

    // kafkahq
    id "com.moowork.node" version "1.2.0"
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.adarshr.test-logger' version '1.6.0'
    id 'com.github.psxpaul.execfork' version '0.1.9'
}

// jooby
apply plugin: "io.spring.dependency-management"
apply plugin: "com.google.osdetector"
apply plugin: "application"
apply plugin: "jooby"
apply plugin: "idea"
apply plugin: "com.commercehub.gradle.plugin.avro"

group "org.kafkahq"
version "0.1"
mainClassName = "org.kafkahq.App"
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://packages.confluent.io/maven/' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jcenter.bintray.com" }
}

lombok {
    version = '1.18.4'
    sha256 = ""
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

configurations {
    implementation {
        exclude  group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude  group: 'log4j', module: 'log4j'
    }
}

/**********************************************************************************************************************\
 * Jooby
 **********************************************************************************************************************/

dependencyManagement {
    imports {
        mavenBom "org.jooby:jooby-bom:$joobyVersion"
        mavenBom "io.micronaut:micronaut-bom:$micronautVersion"
    }
}

// micronaut
configurations {
    developmentOnly
}


/**********************************************************************************************************************\
 * Dependencies
 **********************************************************************************************************************/
dependencies {
    // jooby
    compile "org.jooby:jooby-netty"
    compile "io.netty:netty-transport-native-epoll:${dependencyManagement.importedProperties["netty.version"]}:${osdetector.classifier.contains("linux") ? "linux-x86_64" : ""}"
    compile "io.netty:netty-tcnative-boringssl-static:${dependencyManagement.importedProperties["boringssl.version"]}"
    compile group: "org.jooby", name: "jooby-assets", version: joobyVersion
    compile group: "org.jooby", name: "jooby-livereload", version: joobyVersion
    compile group: "org.jooby", name: "jooby-jackson", version: joobyVersion
    compile group: "org.jooby", name: "jooby-ftl", version: joobyVersion
    compile group: "org.jooby", name: "jooby-whoops", version: joobyVersion

    // kafka
    compile group: "org.apache.kafka", name: "kafka-clients", version: kafkaVersion
    compile group: "io.confluent", name: "kafka-schema-registry-client", version: confluentVersion
    compile group: "io.confluent", name: "kafka-avro-serializer", version: confluentVersion
    compile group: 'org.apache.avro', name: 'avro', version: '1.8.2'

    // utils
    compile group: 'org.codehaus.httpcache4j.uribuilder', name: 'uribuilder', version: '2.0.0'
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'

    // debug
    compile group: "com.google.code.gson", name: "gson", version: "2.8.+"
    compileOnly group: "org.projectlombok", name: "lombok", version: "1.18.+"

    // test
    testCompile "junit:junit:4.12"
    testCompile "com.salesforce.kafka.test:kafka-junit4:3.1.0"
    testCompile "org.apache.kafka:kafka_2.12:" + kafkaVersion
    testCompile "io.rest-assured:rest-assured:3.1.0"
    testCompile "io.confluent:kafka-schema-registry:" + confluentVersion
    testCompile "io.confluent:kafka-schema-registry:" + confluentVersion + ":tests"
    testCompile group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaVersion
    testCompile group: "io.confluent", name: "kafka-streams-avro-serde", version: confluentVersion
    testCompile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.25'

    // micronaut
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-http-client"
    compile "io.micronaut:micronaut-http-server-netty"
    compile "io.micronaut:micronaut-inject"
    compile "io.micronaut:micronaut-validation"
    compile 'io.micronaut:micronaut-views'
    compile "io.micronaut:micronaut-runtime"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime 'org.freemarker:freemarker:2.3.28'

    // micronaut test
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"
}

/**********************************************************************************************************************\
 * Compile & run (micronaut)
 **********************************************************************************************************************/
run.classpath += configurations.developmentOnly
test.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

// use JUnit 5 platform
test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    testLogging {
        exceptionFormat = 'full'
    }
}

testlogger {
    theme 'mocha'
    showExceptions true
    slowThreshold 2000
    showStandardStreams true
}

task testKafkaCluster(type:JavaExec) {
    group = 'verification'
    description = 'Start a standalone test Kafka cluster'
    classpath sourceSets.test.runtimeClasspath
    main = "org.kafkahq.KafkaTestCluster"
}

task testInjectData(type:JavaExec) {
    group = 'verification'
    description = 'Start a standalone test Kafka cluster'
    classpath sourceSets.test.runtimeClasspath
    main = "org.kafkahq.KafkaTestCluster"
    args ['inject']
}


gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(test) || graph.hasTask(testKafkaCluster) || graph.hasTask(testInjectData)) {
        webpack.enabled = false
        npmInstall.enabled = false
    }
}

test.dependsOn "startTestKafkaCluster"

task startTestKafkaCluster(type: com.github.psxpaul.task.JavaExecFork, dependsOn: 'testClasses') {
    group = 'verification'
    description = 'Start a global standalone test Kafka cluster during tests'
    classpath = sourceSets.test.runtimeClasspath
    main = 'org.kafkahq.KafkaTestCluster'
    args ['1']
    waitForOutput = 'Test data injected sleep done'
    timeout = 300
}

/**********************************************************************************************************************\
 * Node + Webpack
 **********************************************************************************************************************/
import com.moowork.gradle.node.task.NodeTask

task webpack(type: NodeTask, dependsOn: "npmInstall") {
    group = 'build'
    description = 'Build with webpack assets'
    script = project.file("node_modules/.bin/webpack")
    outputs.dir("src/main/resources/static")
    args = ["-p", "--mode production"]
}

jar.dependsOn "webpack"
processResources.dependsOn 'webpack'

clean.delete << file("src/main/resources/static")

/**********************************************************************************************************************\
 * Jar
 **********************************************************************************************************************/
shadowJar {
    mergeServiceFiles()
}

/**********************************************************************************************************************\
 * Tmp
 **********************************************************************************************************************/
task micronaut(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.kafkahq.App'
    jvmArgs = ['-Dmicronaut.environments=dev']
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(micronaut)) {
        webpack.enabled = false
        npmInstall.enabled = false
    }
}